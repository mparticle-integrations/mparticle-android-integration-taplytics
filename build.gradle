buildscript {
    ext.kotlin_version = '2.0.20'
    if (!project.hasProperty('version') || project.version.equals('unspecified')) {
        project.version = '+'
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath 'com.mparticle:android-kit-plugin:' + project.version
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.jlleitschuh.gradle.ktlint" version "13.0.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "mparticle-android-integration-taplytics"
        property "sonar.organization", "mparticle"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'kotlin-android'
apply plugin: 'com.mparticle.kit'

repositories {
    maven { url "https://github.com/taplytics/Taplytics-Android-SDK/raw/master/AndroidStudio/" }
    mavenCentral()
}

android {
    namespace 'com.mparticle.kits.taplytics'
    buildFeatures {
        buildConfig = true
    }
    defaultConfig {
        minSdkVersion 21
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    testOptions {
        unitTests.all {
            jvmArgs += ['--add-opens', 'java.base/java.lang=ALL-UNNAMED']
        }
    }
}

dependencies {
    api 'com.taplytics.sdk:taplytics:3.1.0@aar'
}
